plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'org.flywaydb.flyway' version '6.4.3'
  id 'org.springframework.boot' version '2.3.0.RELEASE'
  id 'org.owasp.dependencycheck' version '5.3.2.1'
  id 'com.github.ben-manes.versions' version '0.28.0'
  id 'org.sonarqube' version '3.0'
  id 'info.solidsoft.pitest' version '1.5.1'
}

flyway {
  url = System.getenv('FLYWAY_URL')
  user = System.getenv('FLYWAY_USER')
  password = System.getenv('FLYWAY_PASSWORD')
  baselineOnMigrate = true
  baselineVersion = '000'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.2'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

test {
  failFast = true
}

task functional(type: Test) {
  description = 'Runs functional tests.'
  group = 'Verification'

  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test) {
  description = 'Runs integration tests.'
  group = 'Verification'

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  environment("FTP_PRIVATE_KEY", file('src/integrationTest/resources/keypair').text)
  environment("FTP_PUBLIC_KEY", file('src/integrationTest/resources/keypair.pub').text)
  environment("ENCRYPTION_PUBLIC_KEY", file('src/integrationTest/resources/encryption/pubkey.asc').text)

  failFast = true
}

task smoke(type: Test) {
  description = 'Runs Smoke tests.'
  group = 'Verification'
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.29'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
  toolVersion = "6.17.0"
  ignoreFailures = true
  incrementalAnalysis = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
  afterEvaluate {
    getClassDirectories().from = getClassDirectories().files.collect {
      fileTree(dir: it, exclude: [
        'uk/gov/hmcts/reform/sendletter/config/**',
        '**/BasicLetterInfo.java'
      ])
    }
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport
sonarqube {
  properties {
    property "sonar.projectName", "Reform :: Send Letter Service 2.0"
    property "sonar.coverage.jacoco.xmlReportPaths", jacocoTestReport.reports.xml.destination.path
    property 'sonar.exclusions', "**/config/**, **/BasicLetterInfo.java"
  }
}

pitest {
  pitestVersion = '1.4.9'
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'
  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

repositories {
  jcenter()
  // until new projects get to jcenter
  maven {
    url "https://dl.bintray.com/hmcts/hmcts-maven"
  }
  mavenLocal()
}

def versions = [
  bouncycastle: '1.64',
  junit: '5.5.2',
  junitPlatform: '1.5.2',
  mockitoJupiter: '3.1.0',
  reformLogging: '5.1.0',
  springfoxSwagger: '2.9.2'
]

ext["rest-assured.version"] = '4.1.2'

dependencyManagement {
  dependencies {
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '29.0-jre') {
      entry 'guava'
    }
    // align with jupiter
    dependencySet(group: 'org.mockito', version: versions.mockitoJupiter) {
      entry 'mockito-core'
    }
  }
}

dependencies {
  def withoutJunit4 = {
    exclude group: 'junit', module: 'junit'
  }

  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-mail'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-validation'

  compile group: 'org.flywaydb', name: 'flyway-core', version: '5.2.4'
  compile group: 'org.postgresql', name: 'postgresql', version: '42.2.12'

  compile group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc-template', version: '4.11.1'
  compile group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '4.11.1'

  compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

  compile group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.reformLogging
  compile group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.reformLogging
  compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '3.1.1'
  compile group: 'uk.gov.hmcts.reform', name: 'reform-api-standards', version: '0.4.0'
  compile group: 'uk.gov.hmcts.reform', name: 'pdf-generator', version: '1.0.2'
  compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version:'0.0.4'

  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.3.RELEASE'

  compile group: 'io.github.openfeign', name: 'feign-httpclient', version: '10.2.3'

  compile group: 'com.hierynomus', name: 'sshj', version: '0.29.0'
  compile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.19'
  compile group: 'org.apache.commons', name: 'commons-csv', version: '1.8'

  compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  compile group: 'com.vladmihalcea', name: 'hibernate-types-5', version: '2.9.10'
  compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: versions.bouncycastle
  compile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: versions.bouncycastle
  compile group: 'org.bouncycastle', name: 'bcpg-jdk15on', version: versions.bouncycastle

  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  testCompile group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
  testCompile group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', withoutJunit4
  testCompile group: 'com.icegreen', name: 'greenmail', version: '1.5.13', withoutJunit4
  testCompile group: 'org.apache.commons', name: 'commons-email', version: '1.5'
  testCompile group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
  testCompile group: 'org.apache.pdfbox', name: 'preflight', version: '2.0.19', withoutJunit4
  testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: versions.mockitoJupiter
  testCompile group: 'com.github.tomakehurst', name: 'wiremock-jre8', version: '2.25.1', withoutJunit4

  integrationTestCompile sourceSets.main.runtimeClasspath
  integrationTestCompile sourceSets.test.runtimeClasspath
  integrationTestCompile group: 'org.apache.sshd', name: 'sshd-scp', version: '2.4.0'
  integrationTestCompile group: 'org.apache.sshd', name: 'sshd-sftp', version: '2.4.0'
  integrationTestCompile group: 'org.testcontainers', name: 'postgresql', version: '1.14.2' // brings in junit4. testcontainers depend on it
  integrationTestCompile group: 'org.awaitility', name: 'awaitility', version: '4.0.3', withoutJunit4

  smokeTestCompile sourceSets.main.runtimeClasspath
  smokeTestCompile sourceSets.test.runtimeClasspath
  smokeTestCompile group: 'io.rest-assured', name: 'rest-assured'

  functionalTestCompile sourceSets.main.runtimeClasspath
  functionalTestCompile sourceSets.test.runtimeClasspath
  functionalTestCompile group: 'io.rest-assured', name: 'rest-assured'
}

mainClassName = 'uk.gov.hmcts.reform.sendletter.Application'

bootJar {
  getArchiveFileName().set(provider {
    'send-letter-service.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

import org.flywaydb.gradle.task.FlywayMigrateTask

task migratePostgresDatabase(type: FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
}
