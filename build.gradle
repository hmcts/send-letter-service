import org.flywaydb.gradle.task.FlywayMigrateTask

plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.0'
  id 'org.flywaydb.flyway' version '9.16.1'
  id 'org.springframework.boot' version '2.7.2'
  id 'org.owasp.dependencycheck' version '8.2.1'
  id 'com.github.ben-manes.versions' version '0.46.0'
  id 'org.sonarqube' version '4.0.0.2929'
  id 'info.solidsoft.pitest' version '1.9.11'
  id 'au.com.dius.pact' version '4.4.6'
}

flyway {
  url = System.getenv('FLYWAY_URL')
  user = System.getenv('FLYWAY_USER')
  password = System.getenv('FLYWAY_PASSWORD')
  baselineOnMigrate = true
  baselineVersion = '000'
}

group = 'uk.gov.hmcts.reform'
version = '0.1.0' // 0.1.0 marks migration to java 11

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

test {
  failFast = true
}

task functional(type: Test) {
  description = 'Runs functional tests.'
  group = 'Verification'

  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test) {
  description = 'Runs integration tests.'
  group = 'Verification'

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  environment("FTP_PRIVATE_KEY", file('src/integrationTest/resources/keypair').text)
  environment("FTP_PUBLIC_KEY", file('src/integrationTest/resources/keypair.pub').text)
  environment("ENCRYPTION_PUBLIC_KEY", file('src/integrationTest/resources/encryption/pubkey.asc').text)

  failFast = true
}

task smoke(type: Test) {
  description = 'Runs Smoke tests.'
  group = 'Verification'
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

task contractTest(type: Test) {
  logger.lifecycle("Running contract Tests")
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath

}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

task fortifyScan(type: JavaExec)  {
  mainClass.set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

checkstyle {
  maxWarnings = 0
  toolVersion = '10.9.1'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.55.0"
  ignoreFailures = true
  incrementalAnalysis = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
  afterEvaluate {
    getClassDirectories().from = getClassDirectories().files.collect {
      fileTree(dir: it, exclude: [
        'uk/gov/hmcts/reform/sendletter/config/**',
        '**/BasicLetterInfo.java'
      ])
    }
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport
sonarqube {
  properties {
    property "sonar.projectName", "bulkprint-send-letter-service"
    property "sonar.coverage.jacoco.xmlReportPaths", jacocoTestReport.reports.xml.destination.path
    property 'sonar.exclusions', "**/config/**, **/BasicLetterInfo.java"
  }
}

pitest {
  pitestVersion = '1.4.9'
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'
  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

def versions = [
  bouncycastle: '1.70',
  junit: '5.7.1',
  junitPlatform: '1.7.1',
  reformLogging: '6.0.1',
  springfoxSwagger: '2.9.2',
  pact_version: '4.1.7'
]

ext["rest-assured.version"] = '4.3.0'

configurations.all {
  exclude group: 'org.springframework.security', module: 'spring-security-rsa'
  exclude group: 'org.springframework.boot', module: 'spring-boot-starter-security'
}

dependencyManagement {
  dependencies {
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '31.1-jre') {
      entry 'guava'
    }

    // CVE-2022-36944
    dependencySet(group: 'org.scala-lang', version: '2.13.10') {
      entry 'scala-library'
    }

    // avoid CVE-2021-29425
    dependencySet(group: 'commons-io', version: '2.11.0') {
      entry 'commons-io'
    }

    //CVE-2021-22044
    dependencySet(group: 'org.springframework.cloud', version: '3.1.6') {
      entry 'spring-cloud-openfeign-core'
      entry 'spring-cloud-starter-openfeign'
    }
    dependencySet(group: 'org.apache.logging.log4j', version: '2.20.0') {
      entry 'log4j-api'
      entry 'log4j-to-slf4j'
    }
    //CVE-2022-23181
    dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.73') {
      entry 'tomcat-embed-core'
      entry 'tomcat-embed-websocket'
    }
    //CVE-2022-24823
    dependencySet(group: 'io.netty', version: '4.1.90.Final') {
      entry 'netty-buffer'
      entry 'netty-codec'
      entry 'netty-codec-dns'
      entry 'netty-codec-http'
      entry 'netty-codec-http2'
      entry 'netty-codec-socks'
      entry 'netty-common'
      entry 'netty-handler'
      entry 'netty-handler-proxy'
      entry 'netty-resolver'
      entry 'netty-resolver-dns'
      entry 'netty-resolver-dns-classes-macos'
      entry 'netty-resolver-dns-native-macos'
      entry 'netty-transport'
      entry 'netty-transport-classes-epoll'
      entry 'netty-transport-classes-kqueue'
      entry 'netty-transport-native-epoll'
      entry 'netty-transport-native-kqueue'
      entry 'netty-transport-native-unix-common'
    }
    // CVE-2022-25857
    dependencySet(group: 'org.yaml', version: '1.33') {
      entry 'snakeyaml'
    }
    // CVE-2023-24998
    dependencySet(group: 'commons-fileupload', version: '1.5') {
      entry 'commons-fileupload'
    }
  }
}

dependencies {
  def withoutJunit4 = {
    exclude group: 'junit', module: 'junit'
  }

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-batch'
  implementation group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'

  implementation group: 'org.flywaydb', name: 'flyway-core', version: '8.5.13'
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.6.0'

  implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc-template', version: '5.2.0'
  implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '5.2.0'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.7.0'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.reformLogging
  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '7.3'
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.1.2'
  implementation group: 'com.github.hmcts', name: 'reform-api-standards', version: '0.5.2'
  implementation group: 'com.github.hmcts.rpe-pdf-service', name: 'pdf-generator', version:'1.0.2'

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.10.RELEASE'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.1.6'
  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '12.2'

  implementation group: 'com.hierynomus', name: 'sshj', version: '0.33.0'
  implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.27'
  implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.10.0'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  implementation group: 'com.vladmihalcea', name: 'hibernate-types-5', version: '2.21.1'
  implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: versions.bouncycastle
  implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: versions.bouncycastle
  implementation group: 'org.bouncycastle', name: 'bcpg-jdk15on', version: versions.bouncycastle

  implementation group: 'com.azure', name: 'azure-storage-blob', version: '12.21.0'

  implementation group: 'io.github.resilience4j', name: 'resilience4j-spring-boot2', version: '2.0.2'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  testImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
  testImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  testImplementation group: 'com.icegreen', name: 'greenmail', version: '1.6.14', withoutJunit4
  testImplementation group: 'org.apache.commons', name: 'commons-email', version: '1.5'
  testImplementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
  testImplementation group: 'org.apache.pdfbox', name: 'preflight', version: '2.0.27', withoutJunit4
  testImplementation group: 'com.github.tomakehurst', name: 'wiremock-jre8', version: '2.35.0', withoutJunit4
  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.3.0', classifier: 'all'

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath
  integrationTestImplementation group: 'org.apache.sshd', name: 'sshd-scp', version: '2.9.2'
  integrationTestImplementation group: 'org.apache.sshd', name: 'sshd-sftp', version: '2.9.2'
  integrationTestImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.17.6'
  integrationTestImplementation group: 'org.awaitility', name: 'awaitility', version: '4.2.0', withoutJunit4

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath
  smokeTestImplementation group: 'io.rest-assured', name: 'rest-assured'

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath
  functionalTestImplementation group: 'io.rest-assured', name: 'rest-assured'

  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5', version: versions.pact_version
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'spring', version: versions.pact_version
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5spring', version: versions.pact_version
  contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  contractTestImplementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.1.2'
  contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'

  contractTestImplementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: versions.bouncycastle
  contractTestImplementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: versions.bouncycastle
  contractTestImplementation group: 'org.bouncycastle', name: 'bcpg-jdk15on', version: versions.bouncycastle

  contractTestImplementation group: 'com.github.hmcts.rpe-pdf-service', name: 'pdf-generator', version: '1.0.2'

  contractTestImplementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.27'

  contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  contractTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
}

processFunctionalTestResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

processIntegrationTestResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

mainClassName = 'uk.gov.hmcts.reform.sendletter.Application'

bootJar {
  getArchiveFileName().set(provider {
    'send-letter-service.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

task migratePostgresDatabase(type: FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
}

task runProviderPactVerification(type: Test) {
  logger.lifecycle("Runs pact Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

runProviderPactVerification.dependsOn contractTest

runProviderPactVerification.finalizedBy pactVerify
