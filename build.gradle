plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.7.RELEASE'
  id 'org.flywaydb.flyway' version '5.2.4'
  id 'org.springframework.boot' version '2.1.4.RELEASE'
  id 'org.owasp.dependencycheck' version '4.0.2'
  id 'com.github.ben-manes.versions' version '0.21.0'
  id 'org.sonarqube' version '2.7'
  id 'info.solidsoft.pitest' version '1.4.0'
}

flyway {
  url = System.getenv('FLYWAY_URL')
  user = System.getenv('FLYWAY_USER')
  password = System.getenv('FLYWAY_PASSWORD')
  baselineOnMigrate = true
  baselineVersion = '000'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.2'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

test {
  failFast = true
}

task functional(type: Test) {
  description = 'Runs functional tests.'
  group = 'Verification'

  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test) {
  description = 'Runs integration tests.'
  group = 'Verification'

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
  environment("FTP_PRIVATE_KEY", file('src/integrationTest/resources/keypair').text)
  environment("FTP_PUBLIC_KEY", file('src/integrationTest/resources/keypair.pub').text)
  environment("ENCRYPTION_PUBLIC_KEY", file('src/integrationTest/resources/encryption/pubkey.asc').text)

  failFast = true
}

task smoke(type: Test) {
  description = 'Runs Smoke tests.'
  group = 'Verification'
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.18'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
  toolVersion = "6.12.0"
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
  }
  afterEvaluate {
    getClassDirectories().from(getClassDirectories().files.collect {
      fileTree(dir: it, exclude: [
      'uk/gov/hmcts/reform/sendletter/config/**'
    ])})
  }
}

project.tasks['sonarqube'].dependsOn test, integration
sonarqube {
  properties {
    property "sonar.projectName", "Reform :: Send Letter Service 2.0"
    property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    property "sonar.jacoco.itReportPath", "${project.buildDir}/jacoco/integration.exec"
    property 'sonar.exclusions', "**/config/**"
  }
}

pitest {
  pitestVersion = '1.4.7'
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'
  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

repositories {
  jcenter()
  // until new projects get to jcenter
  maven {
    url "https://dl.bintray.com/hmcts/hmcts-maven"
  }
  mavenLocal()
}

def versions = [
  bouncycastle: '1.61',
  flywayV: plugins.getPlugin('org.flywaydb.flyway').class.package.implementationVersion,
  junit: '5.4.2',
  mockitoJupiter: '2.27.0',
  pdfbox: '2.0.15',
  reformLogging: '5.0.1',
  restAssured: '3.3.0',
  shedlock: '2.4.0',
  springBoot: plugins.getPlugin('org.springframework.boot').class.package.implementationVersion,
  springfoxSwagger: '2.9.2'
]

dependencyManagement {
  dependencies {
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '27.1-jre') {
      entry 'guava'
    }
    // align with jupiter
    dependencySet(group: 'org.mockito', version: versions.mockitoJupiter) {
      entry 'mockito-core'
    }
    // CVE-2019-0232 - command line injections on windows
    dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.19') {
      entry 'tomcat-embed-core'
      entry 'tomcat-embed-el'
      entry 'tomcat-embed-websocket'
    }
  }
}

dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: versions.springBoot

  compile group: 'org.flywaydb', name: 'flyway-core', version: versions.flywayV
  compile group: 'org.postgresql', name: 'postgresql', version: '42.2.5'

  compile group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc-template', version: versions.shedlock
  compile group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: versions.shedlock

  compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

  compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: versions.reformLogging
  compile group: 'uk.gov.hmcts.reform', name: 'java-logging-appinsights', version: versions.reformLogging
  compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '3.0.0'
  compile group: 'uk.gov.hmcts.reform', name: 'reform-api-standards', version: '0.4.0'
  compile group: 'uk.gov.hmcts.reform', name: 'pdf-generator', version: '1.0.2'
  compile group: 'uk.gov.hmcts.reform', name: 'health-spring-boot-starter', version:'0.0.4'
  compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version:'0.0.4'

  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.1.1.RELEASE'

  compile group: 'com.hierynomus', name: 'sshj', version: '0.27.0'
  compile group: 'org.apache.pdfbox', name: 'pdfbox', version: versions.pdfbox
  compile group: 'org.apache.commons', name: 'commons-csv', version: '1.6'

  compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: versions.springBoot
  compile group: 'com.vladmihalcea', name: 'hibernate-types-5', version: '2.4.3'
  compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: versions.bouncycastle
  compile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: versions.bouncycastle
  compile group: 'org.bouncycastle', name: 'bcpg-jdk15on', version: versions.bouncycastle

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot, {
    exclude group: 'junit', module: 'junit'
  }
  testCompile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
  testCompile group: 'org.apache.pdfbox', name: 'preflight', version: versions.pdfbox
  testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: versions.mockitoJupiter
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions.junit

  integrationTestCompile sourceSets.main.runtimeClasspath
  integrationTestCompile sourceSets.test.runtimeClasspath
  integrationTestCompile group: 'org.apache.sshd', name: 'sshd-scp', version: '2.2.0'
  integrationTestCompile group: 'org.apache.sshd', name: 'sshd-sftp', version: '2.2.0'
  integrationTestCompile group: 'org.testcontainers', name: 'postgresql', version: '1.11.2'
  integrationTestCompile group: 'org.awaitility', name: 'awaitility', version: '3.1.6'
  integrationTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions.junit

  smokeTestCompile sourceSets.main.runtimeClasspath
  smokeTestCompile group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
  smokeTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
  smokeTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions.junit

  functionalTestCompile sourceSets.main.runtimeClasspath
  functionalTestCompile group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
  functionalTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
  functionalTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions.junit
}

mainClassName = 'uk.gov.hmcts.reform.sendletter.Application'

bootJar {
  getArchiveFileName().set(provider {
    'send-letter-service.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

import org.flywaydb.gradle.task.FlywayMigrateTask

task migratePostgresDatabase(type: FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
}
